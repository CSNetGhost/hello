# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

include_once ../platforms/fa-lgt.txt
include_once ../scripts/utils.txt

end_time 600 s

platform TEST_AIRCRAFT FA-LGT
   side blue
   
   route
      position   21.325n   158.000w  altitude   20000.0 ft   speed  450.0 kts
      position   20.00n    157.98w   altitude   20000.0 ft   speed  450.0 kts
   end_route
   
   script_variables
      bool keepChecking = true;
      bool allPassed = true;         
   end_script_variables
   
   execute at_time 10 sec absolute
      WsfTurnToHeadingManeuver mvr = WsfTurnToHeadingManeuver.Construct(45.0);
      
      allPassed = ExpectDouble(mvr.GetHeading(), 45.0, 1.0e-6, "The heading should be 45") && allPassed;
      
      WsfP6DOF_Mover mov = (WsfP6DOF_Mover)PLATFORM.Mover();
      mov.ExecuteManeuver(mvr);
   end_execute
   
   execute at_time 598 sec absolute
      allPassed = ExpectDouble(PLATFORM.OrientationNED().X(), 45.0, 0.1, "Heading should be near 45.0") && allPassed;
   end_execute
   

   execute at_time 599 s absolute
      if (allPassed)
      {
         writeln("-PASS-");
      }
      else
      {
         writeln("-FAIL- One or more tests failed.");
      }   
   end_execute     
end_platform