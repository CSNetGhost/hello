# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

include_once ../platforms/fa-lgt.txt
include_once ../scripts/utils.txt

end_time 600 s

script_variables 
   bool allPassed = true;
end_script_variables

platform TEST_AIRCRAFT FA-LGT
   side blue
   
   route
      position   21.325n   158.000w  altitude   20000.0 ft   speed  450.0 kts
      position   20.00n    157.98w   altitude   20000.0 ft   speed  450.0 kts
   end_route
   
   execute at_time 10 sec absolute
      WsfPitchAngleManeuver mvr = WsfPitchAngleManeuver.Construct(10.0);
      mvr.SetEntryConstraint(WsfManeuverConstraint.AT_RELATIVE_TIME(10.0));
      mvr.SetExitConstraint(WsfManeuverConstraint.AT_PITCH_ANGLE(10.0));
      
      allPassed = ExpectDouble(mvr.GetPitchAngle(), 10.0, 0.1, "Angle should be 10.") && allPassed;
      
      WsfP6DOF_Mover mov = (WsfP6DOF_Mover)PLATFORM.Mover();
      mov.ExecuteManeuver(mvr);
   end_execute
   
   execute at_time 12 sec absolute
      allPassed = ExpectDouble(PLATFORM.OrientationNED().Y(), 0.00, 3.0, "Pitch Should be small") && allPassed;
   end_execute
   
   execute at_time 19.9 sec absolute
      allPassed = ExpectDouble(PLATFORM.OrientationNED().Y(), 0.00, 3.0, "Pitch Should be small") && allPassed;
   end_execute
   
   execute at_time 22 sec absolute
      if (PLATFORM.OrientationNED().Y() < 1.0)
      {
         writeln("-FAIL- Pitch should have started.");
      }
   end_execute
   
   execute at_time 44 sec absolute
      allPassed = ExpectDouble(PLATFORM.OrientationNED().Y(), 10.0, 0.1, "Pitch Should be 10") && allPassed;
   end_execute
end_platform

execute at_time 60 s absolute
   if (allPassed)
   {
      writeln("-PASS-");
   }
   else
   {
      writeln("-FAIL- One or more tests failed.");
   }   
end_execute   
