# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

end_time 150 s

include_once ../p6dof_types/environment/p6dof_environment.txt
include_once ../p6dof_types/aircraft/fa-lgt/fa-lgt.txt

script_variables
   int numPassed = 0;
   int expectedPassNum = 12;
   double tolerance = MATH.Pow(10, -6); // 1E-4 % tolerance
end_script_variables

script void pass()
   numPassed += 1;
end_script

script bool CheckValueEqual(double aValue, 
                            double aTargetValue, 
                            double aTolerance, 
                            string aAttributeName, 
                            string aValueUnits, 
                            string aTime)
                       
   double delta = MATH.Fabs(aValue - aTargetValue);
   bool passed = true;
   if (delta >= aTolerance)
   {
      writeln("-FAIL- ", aAttributeName, " = ", aValue, " ", aValueUnits, " is not within tolerance of (", aTolerance, ") from the target of ", aTargetValue, " ", aValueUnits, " at time = ", aTime);
      passed = false;
   }
   return passed;
   
end_script

script bool CheckValueGreater(double aValue, 
                              double aTargetValue, 
                              string aAttributeName, 
                              string aValueUnits, 
                              string aTime)
                       
   double delta = MATH.Fabs(aValue - aTargetValue);
   bool passed = true;
   if (aValue <= aTargetValue)
   {
      writeln("-FAIL- ", aAttributeName, " = ", aValue, " ", aValueUnits, " is not within greater than the target of ", aTargetValue, " ", aValueUnits, " at time = ", aTime);
      passed = false;
   }
   return passed;
   
end_script

platform p6dof_platform WSF_PLATFORM

   altitude 45000 feet
   script void CheckAllValues(string aTime)
      WsfP6DOF_Mover mover = (WsfP6DOF_Mover)Mover();
      
      // Check to make sure that the platform's masses got initialized with something.
      // Masses not greater than 0.0 is an indication of the platforms mass not being set properly.
      // Also, the tolerance is multiplied by the platform's mass, so we should verify that it's not zero.
      if (CheckValueGreater(PLATFORM.FuelMass(), 0.0, "PlatformFuelMass", "kg", aTime)) pass();
      if (CheckValueGreater(PLATFORM.EmptyMass(), 0.0, "PlatformEmptyMass", "kg", aTime)) pass();
      if (CheckValueGreater(PLATFORM.TotalMass(), 0.0, "PlatformTotalMass", "kg", aTime)) pass();
      
      // The platform's masses should be the same as the mover's masses. 
      if (CheckValueEqual(mover.GetTotalFuelRemaining() - PLATFORM.FuelMass(), 0.0, tolerance * PLATFORM.FuelMass(), "FuelMassDifference", "kg", aTime)) pass();
      if (CheckValueEqual(mover.GetEmptyWeight() - PLATFORM.EmptyMass(), 0.0, tolerance * PLATFORM.EmptyMass(), "EmptyMassDifference", "kg", aTime)) pass();
      if (CheckValueEqual(mover.GetTotalWeight() - PLATFORM.TotalMass(), 0.0, tolerance * PLATFORM.TotalMass(), "TotalMassDifference", "kg", aTime)) pass();

   end_script


   add mover WSF_P6DOF_MOVER
      p6dof_object_type FA-LGT
      update_interval 0.01 s
   end_mover
   
   
   on_initialize2 
      CheckAllValues("on_initialize2");
   end_on_initialize2
   
   execute at_time 50 s absolute
      CheckAllValues(write_str(TIME_NOW)); 
   end_execute
  
end_platform

script void check_all_pass()
   int failCount = expectedPassNum - numPassed;
   if (failCount > 0)
   {
      writeln("-FAIL- ", failCount, " tests didn`t pass");
   }
   else if (failCount < 0)
   {
      writeln("-FAIL- More tests passed than expected: ", -failCount);
   }
   else
   {
      writeln("-PASS-");
   }
end_script

execute at_time 120 sec absolute
   check_all_pass();
end_execute