# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# --------------------------------------------------------------------------------------------------
# This is a test of a P6DOF mover over a 1 hour flight
# --------------------------------------------------------------------------------------------------

# test_feature wsf_p6dof

include_once ../platforms/fa-lgt.txt

script double GetTotalFuelRemaining(WsfPlatform aPlatform)
   WsfMover mover = aPlatform.Mover();
   if(mover.IsA_TypeOf("WSF_P6DOF_MOVER"))
   {
      WsfP6DOF_Mover p6dofMover = (WsfP6DOF_Mover)mover;
      return p6dofMover.GetTotalFuelRemaining();
   }
   else
   {
      writeln("--ERROR-- Not a P6DOF Mover.");
      return 0.0;
   }
end_script

# test_feature wsf_p6dof

end_time 3600 sec

script double GetTotalFuelFlowRate(WsfPlatform aPlatform)
   WsfMover mover = aPlatform.Mover();
   if(mover.IsA_TypeOf("WSF_P6DOF_MOVER"))
   {
      WsfP6DOF_Mover p6dofMover = (WsfP6DOF_Mover)mover;
      return p6dofMover.GetTotalFuelFlowRate();
   }
   return 0.0;
end_script

script bool CheckStateData(WsfPlatform aPlatform,
                           double targetFuelFlow_pph,
                           double targetFuelQty_lbs,
                           double targetLat,
                           double targetLon,
                           double targetAlt_ft,
                           double targetSpeed_KTAS,
                           double targetHeading_deg,
                           double targetPitch_deg,
                           double targetRoll_deg)
                           
   double fuelFlow_pph =  GetTotalFuelFlowRate(aPlatform) * Math.LB_PER_KG() * 3600.0;
   double fuelRemaining_lbs = GetTotalFuelRemaining(aPlatform) * Math.LB_PER_KG();
   double lat = aPlatform.Latitude();
   double lon = aPlatform.Longitude();
   double alt_ft = aPlatform.Altitude()*MATH.FT_PER_M();
   double speed_KTAS = aPlatform.Speed()*MATH.NMPH_PER_MPS();
   double heading_deg = aPlatform.Heading();
   double pitch_deg = aPlatform.Pitch();
   double roll_deg = aPlatform.Roll();

   double deltaFuelFlow_pph = MATH.Fabs(fuelFlow_pph-targetFuelFlow_pph);
   double deltaFuelRemaining_lbs = MATH.Fabs(fuelRemaining_lbs-targetFuelQty_lbs);
   double deltaLat = MATH.Fabs(lat-targetLat);
   double deltaLon = MATH.Fabs(lon-targetLon);
   double deltaAlt_ft = MATH.Fabs(alt_ft-targetAlt_ft);
   double deltaSpeed_KTAS = MATH.Fabs(speed_KTAS-targetSpeed_KTAS);
   double deltaHeading_deg = MATH.Fabs(heading_deg-targetHeading_deg);
   double deltaPitch_deg = MATH.Fabs(pitch_deg-targetPitch_deg);
   double deltaRoll_deg = MATH.Fabs(roll_deg-targetRoll_deg);
   
   if(deltaHeading_deg > 180.0)
   {
      deltaHeading_deg -= 360.0;
   }
   else if(deltaHeading_deg < -180.0)
   {
      deltaHeading_deg += 360.0;
   }

   if(deltaRoll_deg > 180.0)
   {
      deltaRoll_deg -= 360.0;
   }
   else if(deltaRoll_deg < -180.0)
   {
      deltaRoll_deg += 360.0;
   }
   
   double debugMultiplier = 1.0; // Set to -1 for testing
   
   // Fuel flow varies significantly with altitude, power setting, and speed as
   // well as autopilot repsonse. A 5% tolerance will be used. For a fuel flow
   // of 5,000 lbs/hr, a 5% error is 250 lbs/hr.
   double toleranceFuelFlow_pph = debugMultiplier * targetFuelFlow_pph*0.05;

   // Fuel quantity should be within 1% or 10 gallons (about 60 lbs),
   // whichever is higher.
   double toleranceFuelRemaining_lbs = targetFuelQty_lbs*0.01;
   if(toleranceFuelRemaining_lbs < 60.0)
   {
      toleranceFuelRemaining_lbs = 60.0;
   }
   toleranceFuelRemaining_lbs *= debugMultiplier;
   
   // For lat/lon, the desired tolerance is 500 feet. Since a nautical mile
   // is approx 0.016667 deg, 500 feet is roughly 0.00137 deg.
   double toleranceLat = debugMultiplier * 0.00137;
   double toleranceLon = debugMultiplier * 0.00137;

   // Altitude should be within 20 feet
   double toleranceAlt_ft = debugMultiplier * 20.0;

   // Speed should be within 1%. Thus, at 450 KTAS, this is 4.5 knots.
   double toleranceSpeed_KTAS = debugMultiplier * targetSpeed_KTAS*0.01;

   // Heading should be within 2 deg
   double toleranceHeading_deg = debugMultiplier * 2.0;

   // Pitch should be within 2 deg
   double tolerancePitch_deg = debugMultiplier * 2.0;
   
   // Roll should be within 2 deg
   double toleranceRoll_deg = debugMultiplier * 2.0;
   
   bool passed = true;

   // Uncomment this line when debugging to add a space line
   //writeln("-FAIL-");
   
   if( deltaFuelFlow_pph > toleranceFuelFlow_pph )
   {
      passed  = false;
      writeln("-FAIL- Fuel Flow = ", fuelFlow_pph, " lbs/hr is not within tolerance of (", toleranceFuelFlow_pph, ") from the target of ", targetFuelFlow_pph, " lbs/hr at time = ", TIME_NOW, " sec");
   }
   
   if( deltaFuelRemaining_lbs > toleranceFuelRemaining_lbs )
   {
      passed  = false;
      writeln("-FAIL- Fuel Qty = ", fuelRemaining_lbs, " lbs is not within tolerance of (", toleranceFuelRemaining_lbs, ") from the target of ", targetFuelQty_lbs, " lbs at time = ", TIME_NOW, " sec");
   }
   
   if( deltaLat > toleranceLat )
   {
      passed  = false;
      writeln("-FAIL- Latitude = ", lat, " deg is not within tolerance of (", toleranceLat, ") from the target of ", targetLat, " deg at time = ", TIME_NOW, " sec");
   }
   
   if( deltaLon > toleranceLon )
   {
      passed  = false;
      writeln("-FAIL- Longitude = ", lon, " deg is not within tolerance of (", toleranceLon, ") from the target of ", targetLon, " deg at time = ", TIME_NOW, " sec");
   }
   
   if( deltaAlt_ft > toleranceAlt_ft )
   {
      passed  = false;
      writeln("-FAIL- Altitude = ", alt_ft, " ft is not within tolerance of (", toleranceAlt_ft, ") from the target of ", targetAlt_ft, " ft at time = ", TIME_NOW, " sec");
   }
   
   if( deltaSpeed_KTAS > toleranceSpeed_KTAS )
   {
      passed  = false;
      writeln("-FAIL- Speed = ", speed_KTAS, " KTAS is not within tolerance of (", toleranceSpeed_KTAS, ") from the target of ", targetSpeed_KTAS, " KTAS at time = ", TIME_NOW, " sec");
   }

   if( deltaHeading_deg > toleranceHeading_deg )
   {
      passed  = false;
      writeln("-FAIL- Heading = ", heading_deg, " deg is not within tolerance of (", toleranceHeading_deg, ") from the target of ", targetHeading_deg, " deg at time = ", TIME_NOW, " sec");
   }
   
   if( deltaPitch_deg > tolerancePitch_deg )
   {
      passed  = false;
      writeln("-FAIL- Pitch = ", pitch_deg, " deg is not within tolerance of (", tolerancePitch_deg, ") from the target of ", targetPitch_deg, " deg at time = ", TIME_NOW, " sec");
   }
   
   if( deltaRoll_deg > toleranceRoll_deg )
   {
      passed  = false;
      writeln("-FAIL- Roll = ", roll_deg, " deg is not within tolerance of (", toleranceRoll_deg, ") from the target of ", targetRoll_deg, " deg at time = ", TIME_NOW, " sec");
   }
   
   if(debugMultiplier < 0.0)
   {
      writeln(" ");
   }
   
   return passed;

end_script

# --------------------------------------------------------------------------------------------------

platform TEST_AIRCRAFT FA-LGT
   side  blue

   script_variables
      bool passed = true;
   end_script_variables

   route
      position   21.325n   158.000w   altitude 2000.0 ft   speed 400.0 kts
      position   21.325n   157.000w   altitude 2000.0 ft   speed 400.0 kts
   end_route
   
   // Ensure the gear are up at start
   landing_gear_down false
   
   // Start with throttle set to full (MIL)
   throttle_full true

   
   execute at_time 120 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        4853.27,   // targetFuelFlow_pph
                                        6838.69,   // targetFuelQty_lbs
                                        21.3255,  // targetLat
                                        -157.762, // targetLon
                                        2001.0,   // targetAlt_ft
                                        400.0,    // targetSpeed_KTAS
                                        89.9,     // targetHeading_deg
                                        1.5,      // targetPitch_deg
                                        0.0);     // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 0
   execute at_time 121 sec absolute 
      PLATFORM.TurnToHeading(0);
   end_execute

   execute at_time 300 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        4830.3,   // targetFuelFlow_pph
                                        6588.49,   // targetFuelQty_lbs
                                        21.6429,  // targetLat
                                        -157.734, // targetLon
                                        2000.9,   // targetAlt_ft
                                        400.0,    // targetSpeed_KTAS
                                        0.0,      // targetHeading_deg
                                        1.47,     // targetPitch_deg
                                        0.0);     // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 270 and climb to 10,000 ft
   execute at_time 301 sec absolute 
      PLATFORM.TurnToHeading(270);
      double alt_m = 10000 * MATH.M_PER_FT(); 
      PLATFORM.GoToAltitude(alt_m);
   end_execute

   execute at_time 600 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        3995.02,   // targetFuelFlow_pph
                                        6076.94,   // targetFuelQty_lbs
                                        21.6592,  // targetLat
                                        -158.315, // targetLon
                                        10001.5,  // targetAlt_ft
                                        400.0,    // targetSpeed_KTAS
                                        -90.0,    // targetHeading_deg
                                        1.99,     // targetPitch_deg
                                        0.02);    // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 180 and increase speed to 500 knots
   execute at_time 601 sec absolute 
      PLATFORM.TurnToHeading(180);
      double spd_mps = 500 * MATH.MPS_PER_NMPH(); 
      PLATFORM.GoToSpeed(spd_mps);
   end_execute

   execute at_time 900 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        5720.14,   // targetFuelFlow_pph
                                        5501.66,  // targetFuelQty_lbs
                                        20.9846,  // targetLat
                                        -158.351, // targetLon
                                        10000.9,  // targetAlt_ft
                                        500.0,    // targetSpeed_KTAS
                                        -180.0,   // targetHeading_deg
                                        1.03,     // targetPitch_deg
                                        0.00);    // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 90 and climb to 30,000 feet
   execute at_time 901 sec absolute 
      PLATFORM.TurnToHeading(90);
      double alt_m = 30000 * MATH.M_PER_FT(); 
      PLATFORM.GoToAltitude(alt_m);
   end_execute

   execute at_time 1200 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        3623.15,   // targetFuelFlow_pph
                                        4803.77,  // targetFuelQty_lbs
                                        20.9622,   // targetLat
                                        -157.633, // targetLon
                                        30002.8,  // targetAlt_ft
                                        500.0,    // targetSpeed_KTAS
                                        90.0,     // targetHeading_deg
                                        2.49,     // targetPitch_deg
                                        -0.02);   // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 0 and slow to 450 knots
   execute at_time 1201 sec absolute 
      PLATFORM.TurnToHeading(0);
      double spd_mps = 450 * MATH.MPS_PER_NMPH(); 
      PLATFORM.GoToSpeed(spd_mps);
   end_execute

   execute at_time 1500 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        3001.1,   // targetFuelFlow_pph
                                        4523.51,  // targetFuelQty_lbs
                                        21.5678,  // targetLat
                                        -157.596,   // targetLon
                                        30003.2,  // targetAlt_ft
                                        450.0,    // targetSpeed_KTAS
                                        0.0,      // targetHeading_deg
                                        3.18,      // targetPitch_deg
                                        0.0);     // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 270 and increase speed to 700 knots
   execute at_time 1501 sec absolute 
      PLATFORM.TurnToHeading(270);
      double spd_mps = 700 * MATH.MPS_PER_NMPH(); 
      PLATFORM.GoToSpeed(spd_mps);
   end_execute

   execute at_time 1700 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        20755.2,  // targetFuelFlow_pph
                                        3210.88,   // targetFuelQty_lbs
                                        21.6052,     // targetLat
                                        -158.229, // targetLon
                                        30001.6,  // targetAlt_ft
                                        699.6,    // targetSpeed_KTAS
                                        -90.0,    // targetHeading_deg
                                        0.88,     // targetPitch_deg
                                        0.05);    // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   // Turn to Heading of 180 and slow to 450 knots
   execute at_time 1701 sec absolute 
      PLATFORM.TurnToHeading(180);
      double spd_mps = 450 * MATH.MPS_PER_NMPH(); 
      PLATFORM.GoToSpeed(spd_mps);
   end_execute
   
   // Turn to Heading of 90
   execute at_time 2000 sec absolute 
      PLATFORM.TurnToHeading(90);
   end_execute
   
   // Turn to Heading of 0
   execute at_time 2300 sec absolute 
      PLATFORM.TurnToHeading(0);
   end_execute
   
   // Turn to Heading of 270
   execute at_time 2600 sec absolute 
      PLATFORM.TurnToHeading(270);
   end_execute
   
   // Turn to Heading of 180
   execute at_time 2900 sec absolute 
      PLATFORM.TurnToHeading(180);
   end_execute

   // Turn to Heading of 90
   execute at_time 3100 sec absolute 
      PLATFORM.TurnToHeading(90);
   end_execute
   
   // Final check
   execute at_time 3590 sec absolute
      bool checkPassed = CheckStateData(PLATFORM,
                                        2781.88,   // targetFuelFlow_pph
                                        1630.45,  // targetFuelQty_lbs
                                        21.1782,  // targetLat
                                        -157.208, // targetLon
                                        30002.8,  // targetAlt_ft
                                        450.0,    // targetSpeed_KTAS
                                        90.0,     // targetHeading_deg
                                        2.748,    // targetPitch_deg
                                        -0.02);   // targetRoll_deg
      if(!checkPassed) { passed = false; }
   end_execute

   script void on_platform_deleted()

      if(passed)
      {
         writeln("-PASS-");
      }
      else
      {
         writeln("-FAIL- One or more tests failed.");
      }
           
   end_script   
   
end_platform

# --------------------------------------------------------------------------------------------------
