# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

include_once ../scripts/utils.txt

include_once ../platforms/fa-lgt.txt

platform_type SIMPLE FA-LGT
   side blue
   icon f-18
   update_interval 0.01 sec
   
   follow_vertical_track
end_platform_type

platform alpha SIMPLE
   route
      position 40:00:00.000n 90:00:00.000w
      altitude 6000 meters
      heading 90 deg
      speed 500 kts
   end_route
end_platform

platform bravo SIMPLE
   route
      position 00:00:00.000n 10:00:00.000w
      altitude 5000 meters
      heading 0 degrees
      speed 500 kts
   end_route
end_platform

platform charlie SIMPLE
   route
      position 00:00:00.000n 20:00:00.000w
      altitude 3000 m
      heading 0 degrees
      speed 500 kts
   end_route
end_platform

platform delta SIMPLE
   route
      position 00:00:00.000n 30:00:00.000w
      altitude 7000 meters
      heading 0 degrees
      speed 500 kts
   end_route
end_platform

formation yankee
   lead section alpha
      lead unit one
         member_platform alpha
      end_unit
      
      unit two
         offset 30 m -100 deg 5 m fixed_altitude
         member_platform bravo
      end_unit
      
      detached
   end_section
   
   section beta
      offset 300 m 90 deg 15 m fixed_altitude
      
      lead unit one
         member_platform charlie
      end_unit
      
      unit two
         offset 30 m 100 deg 5 m fixed_altitude
         member_platform delta
      end_unit
      
      detached
   end_section
   
   detached
end_formation

start_date jun 1 2018
start_time 12:00:00.000
end_time 11 min

script_variables
   WsfFormation alpha;
   WsfFormation beta;
   bool allPassed = true;
end_script_variables

execute at_time 1 min absolute
   alpha = WsfFormationManager.GetFormation("yankee.alpha");
   beta = WsfFormationManager.GetFormation("yankee.beta");
   
   allPassed = ExpectBool(alpha.IsAttached(), false, "yankee.alpha should be detached") && allPassed;
   allPassed = ExpectBool(beta.IsAttached(), false, "yankee.beta should be detached") && allPassed;
end_execute

execute at_time 2 min absolute
   WsfFormationAttachCommand commAlpha = WsfFormationAttachCommand.Construct();
   commAlpha.SetConstraint(WsfFormationCommandConstraint.AT_TIME(130.0));
   alpha.ExecuteCommand(commAlpha);
   
   WsfFormationAttachCommand commBeta = WsfFormationAttachCommand.Construct();
   commBeta.SetConstraint(WsfFormationCommandConstraint.AT_TIME(500.0));
   beta.ExecuteCommand(commBeta);
end_execute

execute at_time 3 min absolute
   allPassed = ExpectBool(alpha.IsAttached(), true, "yankee.alpha should be attached") && allPassed;
   allPassed = ExpectBool(beta.IsAttached(), false, "yankee.beta should be detached") && allPassed;
end_execute

execute at_time 10 min absolute
   allPassed = ExpectBool(alpha.IsAttached(), true, "yankee.alpha should be attached") && allPassed;
   allPassed = ExpectBool(beta.IsAttached(), true, "yankee.beta should be attached") && allPassed;
end_execute

execute at_time 11 min absolute
   if (allPassed)
   {
      writeln("-PASS-");
   } 
   else
   {
      writeln("-FAIL- One or more tests failed.");
   }    
end_execute
