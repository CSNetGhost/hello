# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# Copyright (C) 2021 Stellar Science; U.S. Government has Unlimited Rights.
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

end_time 600 sec

### Sensor ###
sensor GENERIC_EOIR WSF_EOIR_SENSOR
   atmospheric_attenuation 0.005 per m
   band visual
   
   pixel_count  1000 1000
   detection_threshold 3
   track_quality .5
   azimuth_error_sigma 0.45 deg
   elevation_error_sigma  0.45 deg
end_sensor

platform_type GENERIC_SENSOR_SITE WSF_PLATFORM
   processor track_mgr WSF_TRACK_PROCESSOR
   end_processor
   
   processor image_proc WSF_IMAGE_PROCESSOR 
     internal_link track_mgr
   end_processor
   
   multiresolution_sensor mr_sensor WSF_MULTIRESOLUTION_SENSOR
      fidelity 1.0
      
      model low_res_sensor
         fidelity_range 0.0 0.5
         sensor WSF_GEOMETRIC_SENSOR
            internal_link track_mgr
         end_sensor
      end_model
      
      model high_res_sensor
         fidelity_range 0.5 1.0
         sensor GENERIC_EOIR
            internal_link image_proc
         end_sensor
      end_model
      
      common
         on
         reports_location
         frame_time               10 sec
         azimuth_field_of_view   -45.0 deg 45.0 deg  
         elevation_field_of_view -45.0 deg 45.0 deg  
         maximum_range            50 nm
         
         slew_mode                 azimuth_and_elevation
         azimuth_slew_limits     -45 deg 45 deg    
         elevation_slew_limits   -45 deg 45 deg    
         azimuth_slew_rate         5 deg/s
         elevation_slew_rate       5 deg/s 
         selection_mode            single
         ignore_same_side
      end_common
   end_multiresolution_sensor
end_platform_type

platform sensor_site GENERIC_SENSOR_SITE
   side blue
   position 41n 90w
   heading 180 deg
   
   execute at_interval_of 10.0 sec
      int numTracks = PLATFORM.MasterTrackList().Size();
      if((TIME_NOW < 75 || TIME_NOW > 266) && numTracks != 0) { // Regression value for time, should work for either sensor
         writeln("-FAIL- Unexpected track at time ", TIME_NOW);
      } else if((TIME_NOW > 75 && TIME_NOW < 266) && numTracks != 1) {
        writeln("-FAIL- Expected 1 track, got: ", numTracks, ", at time: ", TIME_NOW);
      }
   end_execute
end_platform

### Fighter ###
optical_signature FIGHTER_SPHERICAL_OPTICAL_SIG WSF_COMPOSITE_OPTICAL_SIGNATURE
   surface sphere 
      radius 2 m
      temperature adiabatic_wall
   end_surface
end_optical_signature

platform_type SIMPLE_FIGHTER WSF_PLATFORM
   mover WSF_AIR_MOVER
   end_mover
end_platform_type

platform fighter_multi SIMPLE_FIGHTER
   side red
   optical_signature FIGHTER_SPHERICAL_OPTICAL_SIG

   route
      position 40.2n 89.5w altitude 20000 ft heading 270 deg speed 500 kts
   end_route
end_platform
