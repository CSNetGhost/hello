# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# Copyright (C) 2021 Stellar Science; U.S. Government has Unlimited Rights.
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

test_feature multiresolution

multiresolution_multirun_table
   model_list 
      test sensor my_sensor
      test fuel
   end_model_list
   default_fidelity 0.55 0.55 end_default_fidelity
   loop_after_table_end
   fidelity_table
      0.0  1.0
      0.49 0.5
      0.5  0.49
      1.0  0.0
   end_fidelity_table
end_multiresolution_multirun_table

final_run_number 5

fuel LOW_FIDELITY_FUEL WSF_FUEL
   maximum_quantity 10000 kg
   consumption_rate 1 kg/sec
end_fuel

fuel HIGH_FIDELITY_FUEL WSF_TABULAR_RATE_FUEL
   maximum_quantity 10000 kg
   reserve_quantity 1500  kg
   mode CONSTANT_MODE
 
   fuel_table
      mode CONSTANT_MODE
      constant 1.0 kg/s
   end_fuel_table
end_fuel

sensor LOW_FIDELITY_SENSOR WSF_GEOMETRIC_SENSOR
   on
   frame_time 10 sec
   reports_location
end_sensor

sensor HIGH_FIDELITY_SENSOR WSF_GEOMETRIC_SENSOR
   on
   frame_time 0.1 sec
   reports_location
end_sensor

platform_type SIMPLE_FIGHTER WSF_PLATFORM
   multiresolution_fuel WSF_MULTIRESOLUTION_FUEL     
     model high_fidelity_fuel
        fidelity_range 0.5 1.0
        fuel HIGH_FIDELITY_FUEL
        end_fuel
     end_model
    
     add model low_fidelity_fuel
        fidelity_range 0.0 0.5 
        fuel LOW_FIDELITY_FUEL
        end_fuel
     end_model
   end_multiresolution_fuel
   
   multiresolution_sensor my_sensor WSF_MULTIRESOLUTION_SENSOR
     model high_fidelity_sensor 
        fidelity_range 0.5 1.0
        sensor HIGH_FIDELITY_SENSOR
        end_sensor
     end_model
    
     add model low_fidelity_sensor
        fidelity_range 0.0 0.5
        sensor LOW_FIDELITY_SENSOR
        end_sensor
     end_model
   end_multiresolution_sensor
end_platform_type

platform test SIMPLE_FIGHTER
  on_initialize2
     # Check fuel
     WsfFuel fuel = PLATFORM.Fuel();
     int runNumber = WsfSimulation.RunNumber();
     string expectedFuel;
     string expectedSensor;
     if (runNumber == 1 || runNumber == 2 || runNumber == 5) {
        expectedFuel = "HIGH_FIDELITY_FUEL";
        expectedSensor = "LOW_FIDELITY_SENSOR";
     } else if (runNumber == 3 || runNumber == 4) {
        expectedFuel = "LOW_FIDELITY_FUEL";
        expectedSensor = "HIGH_FIDELITY_SENSOR";
     }
     if(fuel != NULL){
        if(!fuel.IsA_TypeOf(expectedFuel)){
           writeln("-FAIL- Wrong fuel. Expected ", expectedFuel, ", got ", fuel.Type());
        }
     } else {
        writeln("-FAIL- No fuel");
     }
     # Check sensor
     WsfSensor sensor = PLATFORM.Sensor("my_sensor");
     if(sensor != NULL){
        if(!sensor.IsA_TypeOf(expectedSensor)){
           writeln("-FAIL- Wrong sensor. Expected ", expectedSensor, ", got ", sensor.Type());
        }
     } else {
        writeln("-FAIL- No sensor");
     }
  end_on_initialize2
end_platform
