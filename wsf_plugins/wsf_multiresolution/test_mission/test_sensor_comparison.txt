# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# Copyright (C) 2021 Stellar Science; U.S. Government has Unlimited Rights.
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# Tests that we get identical results when using identical sensors, but with one 
# attached to a platform in the standard way and one using multiresolution_sensor.

end_time 600 sec

### Sensor sites ###
sensor GENERIC_IRST WSF_IRST_SENSOR
   atmospheric_attenuation   0.05 per km
   background_radiance       500 microwatts/cm^2/sr
   band                      medium
   noise_equivalent_irradiance  1.0E-12 w/cm^2
   detection_threshold         3.0

   azimuth_error_sigma         0.001 deg
   elevation_error_sigma       0.001 deg
   range_error_sigma           7.0 m
   hits_to_establish_track     3 5
   hits_to_maintain_track      1 4
end_sensor

# Platform type containing the multiresolution sensor definition
platform_type MR_SENSOR_SITE WSF_PLATFORM
   processor track_mgr WSF_TRACK_PROCESSOR
   end_processor
   
   processor image_proc WSF_IMAGE_PROCESSOR 
     internal_link track_mgr
   end_processor
   
   multiresolution_sensor mr_sensor WSF_MULTIRESOLUTION_SENSOR
      model low_res_sensor
         fidelity_range 0.0 0.5
         sensor WSF_GEOMETRIC_SENSOR
            internal_link track_mgr
         end_sensor
      end_model
      
      model high_res_sensor
         fidelity_range 0.5 1.0
         sensor GENERIC_IRST
            internal_link image_proc
         end_sensor
      end_model
      
      common
         on
         reports_range
         frame_time               10 sec
         azimuth_field_of_view   -45.0 deg 45.0 deg  
         elevation_field_of_view -45.0 deg 45.0 deg  
         maximum_range            50 nm
         
         slew_mode                 azimuth_and_elevation
         azimuth_slew_limits     -45 deg 45 deg    
         elevation_slew_limits   -45 deg 45 deg    
         azimuth_slew_rate         5 deg/s
         elevation_slew_rate       5 deg/s 
         selection_mode            single
         ignore_same_side
      end_common
   end_multiresolution_sensor
end_platform_type

# Platform type for comparison using a geometric sensor
platform_type GEOMETRIC_SENSOR_SITE WSF_PLATFORM
   processor track_mgr WSF_TRACK_PROCESSOR
   end_processor
   sensor geom_sensor WSF_GEOMETRIC_SENSOR
      internal_link track_mgr
      on
      
      reports_range
      frame_time               10 sec
      azimuth_field_of_view   -45.0 deg 45.0 deg  
      elevation_field_of_view -45.0 deg 45.0 deg  
      maximum_range            50 nm
      slew_mode                 azimuth_and_elevation
      azimuth_slew_limits     -45 deg 45 deg    
      elevation_slew_limits   -45 deg 45 deg    
      azimuth_slew_rate         5 deg/s
      elevation_slew_rate       5 deg/s 
      selection_mode            single
      ignore_same_side
   end_sensor
end_platform_type

platform mr_sensor_site MR_SENSOR_SITE
   icon Ground_Radar
   multiresolution_sensor mr_sensor
      fidelity 0
   end_multiresolution_sensor
   side blue
   position 40n 90w
   heading 0 deg
end_platform

platform geometric_sensor_site GEOMETRIC_SENSOR_SITE
   icon Ground_Radar
   side blue
   position 40n 90w
   heading 0 deg
end_platform

### Fighter platform ###
optical_signature FIGHTER_SPHERICAL_OPTICAL_SIG WSF_COMPOSITE_OPTICAL_SIGNATURE
   surface sphere 
      radius 2 m
      temperature adiabatic_wall
   end_surface
end_optical_signature

platform_type SIMPLE_FIGHTER WSF_PLATFORM
   mover WSF_AIR_MOVER
   end_mover
end_platform_type

platform fighter SIMPLE_FIGHTER
   icon f-18
   side red
   optical_signature FIGHTER_SPHERICAL_OPTICAL_SIG

   route
      position 40.2n 89.5w altitude 20000 ft heading 270 deg speed 500 kts
   end_route
end_platform

### Observer for testing ###
script_variables
   double mrSensorRange = 0.0;
   bool mrSensorHasUpdate = false;
   double geomSensorRange = 0.0;
   bool geomSensorHasUpdate = false;
end_script_variables

script void SensorTrackUpdated(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack)
   if(mrSensorHasUpdate && aPlatform.Name() == "mr_sensor_site") {
      writeln("-FAIL- Got two mr_sensor_site track updates, expected a geometric_sensor_site track update");
   }
   if(geomSensorHasUpdate && aPlatform.Name() == "geometric_sensor_site") {
      writeln("-FAIL- Got two geometric_sensor_site track updates, expected an mr_sensor_site track update");
   }
   
   if(!geomSensorHasUpdate && aPlatform.Name() == "geometric_sensor_site") {
      geomSensorRange = aTrack.Range();
      geomSensorHasUpdate = true;
   } else if(!mrSensorHasUpdate && aPlatform.Name() == "mr_sensor_site") {
      mrSensorRange = aTrack.Range();
      mrSensorHasUpdate = true;
   }
   if(mrSensorHasUpdate && geomSensorHasUpdate){
      if(mrSensorRange != geomSensorRange){
         writeln("Ranges don't match: MR: ", mrSensorRange, ", geometric: ", geomSensorRange);
         writeln("-FAIL-");
      }
      mrSensorHasUpdate = false;
      geomSensorHasUpdate = false;
   }
end_script

observer
   enable SENSOR_TRACK_UPDATED
end_observer