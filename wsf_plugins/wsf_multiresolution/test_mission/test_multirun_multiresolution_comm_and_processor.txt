# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# Copyright (C) 2021 Stellar Science; U.S. Government has Unlimited Rights.
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

test_feature multiresolution

multiresolution_multirun_table
   model_list 
      test comm my_comm
      test processor my_processor
   end_model_list
   default_fidelity 0.55 0.45 end_default_fidelity
   fidelity_table
      0.0  1.0
      0.49 0.5
      0.5  0.49
      1.0  0.0
   end_fidelity_table
end_multiresolution_multirun_table

final_run_number 5

comm LOW_FIDELITY_COMM WSF_COMM_TRANSCEIVER
end_comm

comm HIGH_FIDELITY_COMM WSF_COMM_TRANSCEIVER
end_comm

processor LOW_FIDELITY_PROCESSOR WSF_PERFECT_TRACKER   
end_processor

processor HIGH_FIDELITY_PROCESSOR WSF_PERFECT_TRACKER
end_processor

platform_type SIMPLE_FIGHTER WSF_PLATFORM
   multiresolution_processor my_processor WSF_MULTIRESOLUTION_PROCESSOR     
     model high_fidelity_processor
        fidelity_range 0.5 1.0
        processor HIGH_FIDELITY_PROCESSOR
        end_processor
     end_model
    
     add model low_fidelity_processor
        fidelity_range 0.0 0.5 
        processor LOW_FIDELITY_PROCESSOR
        end_processor
     end_model
   end_multiresolution_processor
   
   multiresolution_comm my_comm WSF_MULTIRESOLUTION_COMM
     model high_fidelity_comm 
        fidelity_range 0.5 1.0
        comm HIGH_FIDELITY_COMM
        end_comm
     end_model
    
     add model low_fidelity_comm
        fidelity_range 0.0 0.5
        comm LOW_FIDELITY_COMM
        end_comm
     end_model
   end_multiresolution_comm
end_platform_type

platform test SIMPLE_FIGHTER
  on_initialize2
     # Check processor
     WsfProcessor processor = PLATFORM.Processor("my_processor");
     int runNumber = WsfSimulation.RunNumber();
     string expectedProcessor;
     string expectedComm;
     if (runNumber == 1 || runNumber == 2) {
        expectedProcessor = "HIGH_FIDELITY_PROCESSOR";
        expectedComm = "LOW_FIDELITY_COMM";
     } else if (runNumber == 3 || runNumber == 4 || runNumber == 5) {
        expectedProcessor = "LOW_FIDELITY_PROCESSOR";
        expectedComm = "HIGH_FIDELITY_COMM";
     }
     if(processor != NULL){
        if(!processor.IsA_TypeOf(expectedProcessor)){
           writeln("-FAIL- Wrong processor. Expected ", expectedProcessor, ", got ", processor.Type());
        }
     } else {
        writeln("-FAIL- No processor");
     }
     # Check comm
     WsfComm comm = PLATFORM.Comm("my_comm");
     if(comm != NULL){
        if(!comm.IsA_TypeOf(expectedComm)){
           writeln("-FAIL- Wrong comm. Expected ", expectedComm, ", got ", comm.Type());
        }
     } else {
        writeln("-FAIL- No comm");
     }
  end_on_initialize2
end_platform
