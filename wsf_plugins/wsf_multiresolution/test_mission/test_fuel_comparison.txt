# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# Copyright (C) 2021 Stellar Science; U.S. Government has Unlimited Rights.
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# Tests that we get identical results when using identical fuel models, but with 
# one attached to a platform in the standard way and one using multiresolution_fuel.

end_time 7 min

fuel LOW_FIDELITY_FUEL WSF_FUEL
   maximum_quantity 10000 kg
   consumption_rate 1 kg/sec
end_fuel

fuel HIGH_FIDELITY_FUEL WSF_TABULAR_RATE_FUEL
   maximum_quantity 10000 kg
   reserve_quantity 1500  kg
   mode TEST_MODE
 
   # Values from fuel_table_demo
   fuel_table
      mode TEST_MODE
      altitudes
         units m
         0 5000 10000
      end_altitudes
      mach
        0.25 0.5 0.75 1.0
      end_mach
      rates
         units kg/s
        # .25 0.5  .75  1.0    mach
         3.0  4.5  5.5  7.0  # Sea Level
         2.0  3.5  4.5  6.0  # 5000 m
         1.0  2.5  3.5  5.0  # 10000 m
      end_rates
   end_fuel_table
end_fuel

platform_type SIMPLE_FIGHTER WSF_PLATFORM
   icon F-18
   add mover WSF_AIR_MOVER
      update_interval 5.0 s
   end_mover
   route
      position 0n 0e altitude 30000 ft speed 300 kts
      position 1n 0e altitude  5000 ft speed 600 kts
   end_route
end_platform_type

platform_type MR_FUEL_FIGHTER SIMPLE_FIGHTER
   multiresolution_fuel WSF_MULTIRESOLUTION_FUEL     
     model high_fidelity_fuel
        fidelity_range 0.5 1.0
        fuel HIGH_FIDELITY_FUEL
        end_fuel
     end_model
    
     add model low_fidelity_fuel
        fidelity_range 0.0 0.5 
        fuel LOW_FIDELITY_FUEL
        end_fuel
     end_model
   end_multiresolution_fuel
end_platform_type

platform mr_fuel_platform MR_FUEL_FIGHTER
   multiresolution_fuel
      fidelity 1
   end_multiresolution_fuel
end_platform

platform tabular_fuel_platform SIMPLE_FIGHTER
   add fuel HIGH_FIDELITY_FUEL
   end_fuel
end_platform

script_variables
   double mrFuelMass = 0.0;
   bool mrFuelHasUpdate = false;
   double tabularFuelMass = 0.0;
   bool tabularFuelHasUpdate = false;
end_script_variables

script void MoverUpdated(WsfPlatform aPlatform, WsfMover aMover)
   if(mrFuelHasUpdate && aPlatform.Name() == "mr_fuel_platform") {
      writeln("-FAIL- Got two mr_fuel_platform updates, expected a tabular_fuel_platform update");
   }
   if(tabularFuelHasUpdate && aPlatform.Name() == "tabular_fuel_platform") {
      writeln("-FAIL- Got two tabular_fuel_platform updates, expected an mr_fuel_platform update");
   }
   
   WsfFuel fuel = aPlatform.Fuel();
   if(!tabularFuelHasUpdate && aPlatform.Name() == "tabular_fuel_platform") {
      tabularFuelMass = fuel.QuantityRemaining();
      tabularFuelHasUpdate = true;
   } else if(!mrFuelHasUpdate && aPlatform.Name() == "mr_fuel_platform") {
      mrFuelMass = fuel.QuantityRemaining();
      mrFuelHasUpdate = true;
   }
   
   if(mrFuelHasUpdate && tabularFuelHasUpdate){
      if(mrFuelMass != tabularFuelMass){
         writeln("Locations don't match: MR: ", mrFuelMass, ", air: ", tabularFuelMass);
         writeln("-FAIL-");
      } else {
         writeln("-PASS- Fuel masses match");
      }
      mrFuelHasUpdate = false;
      tabularFuelHasUpdate = false;
   }
end_script

observer
   enable MOVER_UPDATED
end_observer
