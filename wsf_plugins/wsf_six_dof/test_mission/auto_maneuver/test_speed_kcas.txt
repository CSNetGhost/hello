# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

include_once ../platforms/fa-lgt.txt
include_once ../scripts/utils.txt

end_time 600 s

platform TEST_AIRCRAFT FA-LGT_RB6
   side blue
   
   route
      position   21.325n   158.000w  altitude   20000.0 ft   speed  450.0 kts
      position   20.00n    157.98w   altitude   20000.0 ft   speed  450.0 kts
   end_route
   
   script_variables
      double initialSpeed;
      bool allPassed = true;    
   end_script_variables
   
   execute at_time 10 sec absolute
      WsfSixDOF_SpeedKCAS_Maneuver mvr = WsfSixDOF_SpeedKCAS_Maneuver.Construct(300.0);
      mvr.SetEntryConstraint(WsfSixDOF_ManeuverConstraint.AT_RELATIVE_TIME(10.0));
      mvr.SetExitConstraint(WsfSixDOF_ManeuverConstraint.AT_RELATIVE_TIME(3.0));
      
      allPassed = ExpectDouble(mvr.GetSpeed(), 300.0, 1.0e-4, "KCAS speed should be 300.0") && allPassed;

      initialSpeed = PLATFORM.Speed();

      WsfRigidBodySixDOF_Mover mov = (WsfRigidBodySixDOF_Mover)PLATFORM.Mover();      
      mov.ExecuteManeuver(mvr);
   end_execute
   
   execute at_time 12 sec absolute
      allPassed = ExpectDouble(PLATFORM.Speed(), initialSpeed, 1.0, "Speed should not have changed") && allPassed;
   end_execute
   
   execute at_time 19.9 sec absolute
      allPassed = ExpectDouble(PLATFORM.Speed(), initialSpeed, 1.0, "Speed should not have changed during pending") && allPassed;
   end_execute
   
   execute at_time 500 sec absolute
      allPassed = ExpectDouble(PLATFORM.Speed(), 211.0, 1.0, "Speed should match.") && allPassed;
   end_execute
   
   execute at_time 550 s absolute
      if (allPassed)
      {
         writeln("-PASS-");
      }
      else
      {
         writeln("-FAIL- One or more tests failed.");
      }   
   end_execute        
end_platform