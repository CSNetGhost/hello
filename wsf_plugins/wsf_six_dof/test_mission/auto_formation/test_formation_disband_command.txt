# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

include_once default_formation.txt
include_once ../scripts/utils.txt

start_date jun 1 2018
start_time 12:00:00.000
end_time 6 min

script_variables 
   bool allPassed = true;
end_script_variables

execute at_time 1 min absolute
   WsfSixDOF_Formation alpha = WsfSixDOF_FormationManager.GetFormation("yankee.alpha");
   allPassed = ExpectBool(alpha.ExecuteCommand(WsfSixDOF_FormationDisbandCommand.Construct()),
              false,
              "cannot execute disband on non-root formations") && allPassed;
end_execute

execute at_time 2 min absolute
   Array<WsfSixDOF_Formation> forms = WsfSixDOF_FormationManager.GetTopLevelFormations();
   allPassed = ExpectInt(forms.Size(), 2, "should have two top-level formations") && allPassed;
   
   
   WsfSixDOF_FormationDisbandCommand comm1 = WsfSixDOF_FormationDisbandCommand.Construct();
   comm1.SetConstraint(WsfSixDOF_FormationCommandConstraint.AT_TIME(180.0));
   WsfSixDOF_FormationManager.GetFormation("yankee").ExecuteCommand(comm1);
   
   WsfSixDOF_FormationDisbandCommand comm2 = WsfSixDOF_FormationDisbandCommand.Construct();
   comm2.SetConstraint(WsfSixDOF_FormationCommandConstraint.AT_TIME(240.0));
   WsfSixDOF_FormationManager.GetFormation("zulu").ExecuteCommand(comm2);
end_execute

execute at_time 3.5 min absolute
   WsfSixDOF_Formation yankee = WsfSixDOF_FormationManager.GetFormation("yankee");
   WsfSixDOF_Formation zulu = WsfSixDOF_FormationManager.GetFormation("zulu");
   allPassed = ExpectBool(yankee.IsValid(), false, "yankee should have been disbanded") && allPassed;
   allPassed = ExpectBool(zulu.IsValid(), true, "zulu should still exist") && allPassed; 
end_execute

execute at_time 4.5 min absolute
   WsfSixDOF_Formation yankee = WsfSixDOF_FormationManager.GetFormation("yankee");
   WsfSixDOF_Formation zulu = WsfSixDOF_FormationManager.GetFormation("zulu");
   allPassed = ExpectBool(yankee.IsValid(), false, "yankee should have been disbanded") && allPassed;
   allPassed = ExpectBool(zulu.IsValid(), false, "zulu should have been disbanded") && allPassed; 
end_execute

execute at_time 5.5 min absolute
   if (allPassed)
   {
      writeln("-PASS-");
   }
   else
   {
      writeln("-FAIL- One or more tests failed.");
   }     
end_execute