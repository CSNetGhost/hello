# ****************************************************************************
# CUI//REL TO USA ONLY
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************
# This tests that only one track is being generated from the main sensor
# in the baseline case of 4 detecting senors and 1 ground emitting target.

define_path_variable CASE test_trimsim_track

# ****************************************************************************
radar_signature TEST_RADAR_SIGNATURE constant 10 m^2 end_radar_signature

script_debug_writes off

platform_type PASSIVE_SNSR_PLATFORM WSF_PLATFORM

   icon f-35
   radar_signature TEST_RADAR_SIGNATURE

   mover WSF_AIR_MOVER
   end_mover

   sensor passive WSF_ESM_SENSOR
      on
      frame_time                   1.0 sec
      reports_location
      ignore_same_side

      receiver
         frequency_band            2.01 ghz 34 ghz
         bandwidth                 2.0 mhz
      end_receiver

      error_model trimsim_error
         north_position_error_sigma      0.1 ft
         east_position_error_sigma       0.1 ft
         down_position_error_sigma       0.1 ft
         reference_time_error            5.0 ns
         inter_system_time_delay         5.0 ns
#         ground_target_altitude_error    0.1 ft
      end_error_model

   end_sensor

end_platform_type

# ****************************************************************************
platform p1 PASSIVE_SNSR_PLATFORM
   route
      label start
         position 36:04:55n 115:08:38w altitude 30000 ft msl speed 400 kts
         position 38:04:55n 115:08:38w altitude 30000 ft msl speed 400 kts
      goto start
   end_route
   add processor tdoa WSF_TRIMSIM_PROCESSOR
#      debug
#      debug_level 0
      minimum_detections 4    // default value
      sensors
         sensor passive
         platform_sensor p2 passive
         platform_sensor p3 passive
         platform_sensor p4 passive         
      end_sensors
   end_processor
end_platform

platform p2 PASSIVE_SNSR_PLATFORM
    route
      label start
         position 36:04:55n 114:36:38w altitude 30000 ft msl speed 400 kts
         position 38:04:55n 114:36:38w altitude 30000 ft msl speed 400 kts
      goto start
   end_route
end_platform

platform p3 PASSIVE_SNSR_PLATFORM
   route
      label start
         position 36:04:55n 115:34:38w altitude 30000 ft msl speed 400 kts
         position 38:04:55n 115:34:38w altitude 30000 ft msl speed 400 kts
      goto start
   end_route
end_platform

platform p4 PASSIVE_SNSR_PLATFORM
   route
      label start
         position 36:04:55n 116:02:38w altitude 30000 ft msl speed 400 kts
         position 38:04:55n 116:02:38w altitude 30000 ft msl speed 400 kts
      goto start
   end_route
end_platform

# ****************************************************************************
platform target WSF_PLATFORM
   icon Fuel_Truck
   side blue
   spatial_domain land
   position 37:22:24.13n 115:31:19.32w altitude 1.00 ft agl
   add sensor radar WSF_RADAR_SENSOR
      on
      frame_time              1.0 sec
      transmitter
         frequency            9.6 ghz
         power                2.3 MW
      end_transmitter
      reports_location
      ignore_same_side
   end_sensor
end_platform

# ****************************************************************************
end_time 5 s

script void CheckDetectors(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack,
                           Array<string> aExpectedPlatforms, string aExpectedDetectors)
   if (aSensor.Name() == "passive")
   {
      bool platformFound = false;
      for (int i = 0; i < aExpectedPlatforms.Size(); i += 1)
      {
         if (aPlatform.Name() == aExpectedPlatforms[i])
         {
            platformFound = true;
            break;
         }
      }

      if (platformFound)
      {
         string tdoa_detectors = aTrack.AuxDataString("tdoa_detectors");
         if (tdoa_detectors == aExpectedDetectors)
         {
            writeln("-PASS- ", tdoa_detectors);
         }
         else writeln("-FAIL- ", tdoa_detectors);
      }
      else writeln("-FAIL- ", aPlatform.Name());
   }
end_script

script void SensorTrackInitiated(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack)
   if (aSensor.Name() == "passive")
   {
      writeln_d(Format.Fixed(TIME_NOW, 5), " SENSOR_TRACK_INITIATED ", aPlatform.Name(), ".", aSensor.Name());
      # Tracks are only expected to be generated by the main sensor 
      # even though all secondary sensors are detecting the target emitter
      CheckDetectors(aPlatform, aSensor, aTrack, { "p1" }, " p1.passive p2.passive p3.passive p4.passive");
   }
end_script

script void SensorTrackUpdated(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack)
   if (aSensor.Name() == "passive")
   {
      writeln_d(Format.Fixed(TIME_NOW, 5), " SENSOR_TRACK_UPDATED ", aPlatform.Name(), ".", aSensor.Name());
      # Tracks are only expected to be generated by the main sensor 
      # even though all secondary sensors are detecting the target emitter      
      CheckDetectors(aPlatform, aSensor, aTrack, { "p1" }, " p1.passive p2.passive p3.passive p4.passive");
   }
end_script

observer
   enable SENSOR_TRACK_INITIATED
   enable SENSOR_TRACK_UPDATED
end_observer
