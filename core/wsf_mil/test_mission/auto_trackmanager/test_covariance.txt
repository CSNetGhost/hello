# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# Test the state covariance script methods.

platform target WSF_PLATFORM
   side blue
   icon Wedge
   add mover WSF_AIR_MOVER
   end_mover

   route
      position 40n 88w altitude 30000 ft heading 270 deg speed 300 kts
   end_route
end_platform

platform sensor WSF_PLATFORM
   side green
   icon Ground_Control_Radar
   position 40n 90w heading 90 deg

   track_manager
      filter WSF_KALMAN_FILTER
      end_filter
      fusion_method weighted_average
   end_track_manager

   add processor data_mgr WSF_TRACK_PROCESSOR
      purge_interval 60 sec
   end_processor

   add sensor radar WSF_GEOMETRIC_SENSOR
      frame_time             10 sec
      range_error_sigma      100 m
      azimuth_error_sigma    0.01 deg
      elevation_error_sigma  0.01 deg
      reports_location
      internal_link data_mgr
      on
   end_sensor

   add processor track_checker WSF_TASK_PROCESSOR
      evaluation_interval DETECTED 15.0 sec

      script void PrintCovariance(WsfCovariance aCovariance)
         write("     Size: ", aCovariance.Rows(), " x ", aCovariance.Columns());
         write(" Bearing: ", aCovariance.Bearing(), " deg");
         write(" Major Axis: ", aCovariance.MajorAxis(), " m");
         write(" Minor Axis: ", aCovariance.MinorAxis(), " m");
         write(" Trace: ", aCovariance.Trace());
         writeln("");
      end_script

      state DETECTED
         on_entry
            writeln("T=", TIME_NOW, " Detected ", TRACK.TargetName());
         end_on_entry

         next_state DETECTED
            writeln("T=", TIME_NOW, " Checking ", TRACK.TargetName());
            writeln("  Last Update Time: ", TRACK.UpdateTime());

            WsfCovariance covarNow   = TRACK.StateCovariance();
            writeln("  Covariance at time of last update: ");
            PrintCovariance(covarNow);

            WsfCovariance covarLater = TRACK.StateCovarianceAtTime(TIME_NOW + 10.0);
            writeln("  Covariance at T=", TIME_NOW + 10.0);
            PrintCovariance(covarLater);

            return true;
         end_next_state
      end_state
   end_processor
end_platform

end_time 121 sec

