# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

// Test to insure RouteMover correctly pauses to velocity 0 mid route
//   by testing Latitude of platform with a 0 degree heading
//

end_time 150 s

#event_pipe
#   file test_pause_mid_route_verify_lat_lon.aer
#end_event_pipe

platform test_plat WSF_PLATFORM
   add mover WSF_GROUND_MOVER
      update_interval 1 s  // otherwise mover never updates
      route
         position 30n 90w speed 20 m/s
         position 30:00:10n 90w speed 20 m/s
         position 30:00:20n 90w speed 20 m/s
            pause_time 30 s
            execute paused
         position 30:00:40n 90w speed 20 m/s
            execute velocity20AfterPause
         position 30:00:50n 90w speed 20 m/s
      end_route
   end_mover

   script_variables
      bool pass = true;
      int scriptExecuteCount = 2;  // Insure appropriate scripts are executed for test
      double pauseLat;
   end_script_variables
   script void velocity0()
      writeln(TIME_NOW, " Checking for no change in Latitude");
      scriptExecuteCount = scriptExecuteCount - 1;
      if (Latitude() != pauseLat)
      {
         writeln("-FAIL- Platform not at correct latitude 30:00:20n - actual ", Latitude());
      }
      pass = pass && (Latitude() == pauseLat);
   end_script
   script void paused()
      writeln(TIME_NOW, " Paused at Latitude: ", Latitude());
      pauseLat = Latitude();
      // Verify Latitude is maintained 20 s later
      ExecuteAtTime(TIME_NOW + 20, "velocity0");
   end_script
   script void velocity20AfterPause()
      writeln(TIME_NOW, " After");
      scriptExecuteCount = scriptExecuteCount - 1;
      if (Speed() != 20)
      {
         writeln("-FAIL- Pause Velocity != 20, actual ", Speed());
      }
      pass = pass && (Speed() == 20);
   end_script
   script void on_platform_deleted()
      writeln((Latitude() - 30) * 3600);
      if (scriptExecuteCount != 0)
      {
         writeln("-FAIL- ", scriptExecuteCount, " script callbacks failed to execute");
      }
      else if (pass)
      {
         writeln("-PASS-");
      }
   end_script
end_platform

