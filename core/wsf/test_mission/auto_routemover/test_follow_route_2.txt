# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

// Test mover FollowRoute(route2)
//    first waypoint should be passed
//
//
//    Initial route purposely large than 2nd to test proper
//    index updating
//
///////////////////////////////////

end_time 30 s

route route1
   position 29:59:50n 90w speed 50 mph
   position 30n 90w speed 50 mph
   position 30n 90:00:05w speed 50 mph
      label start
   position 30n 90:00:07w speed 50 mph
   position 30n 90:00:10w speed 50 mph
      execute end_route1
end_route

route route2
   position 30:00:10n 90:00:19w speed 50 mph
      execute pass_script
   position 30:00:00n 90:00:19w speed 50 mph
      execute temp
end_route

platform test_plat WSF_PLATFORM
   add mover WSF_GROUND_MOVER
      start_at start
      update_interval 1 s // otherwise mover never updates
   end_mover
   use_route route1
   script void end_route1()
      writeln(TIME_NOW, " Follow route2");
      PLATFORM.FollowRoute("route2");
   end_script
   script_variables
      bool pass = false;
   end_script_variables
   script void pass_script()
      writeln(TIME_NOW, " pass_script");
      pass = true;
   end_script
   script void temp()
      writeln(TIME_NOW, " temp");
   end_script
   script void on_platform_deleted()
      if (pass)
      {
         writeln("-PASS-");
      }
      else
      {
         writeln("-FAIL-");
      }
   end_script
end_platform

