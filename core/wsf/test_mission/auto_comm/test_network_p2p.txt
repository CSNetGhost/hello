# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

script_variables
   bool testPass = false;
end_script_variables

comm TEST_COMM WSF_COMM_TRANSCEIVER
end_comm

processor MESSAGE_PROC WSF_SCRIPT_PROCESSOR
   on_message
      script
         writeln("MESSAGE RECEIVED ON: " + PLATFORM.Name());
         testPass = true;
      end_script
   end_on_message
end_processor

network network_p2p WSF_COMM_NETWORK_P2P
   network_address 111.111.0.0/24
   address_list
      member 111.111.0.1/24
      member 111.111.0.2/24
   end_address_list
end_network

network network_p2p_unused WSF_COMM_NETWORK_P2P
end_network

platform_type TEST_PLAT WSF_PLATFORM
   add comm comm1 TEST_COMM
      internal_link proc1
   end_comm
   add processor proc1 MESSAGE_PROC
   end_processor
end_platform_type

platform test1 TEST_PLAT
   edit comm comm1
      address 111.111.0.1/24
   end_comm
end_platform

platform test2 TEST_PLAT
   edit comm comm1
      address 111.111.0.2/24
   end_comm
end_platform

execute at_time 2 s absolute
   WsfMessage testMessage = WsfMessage();
   testMessage.SetType("TEST_MESSAGE");

   WsfPlatform sendPlat = WsfSimulation.FindPlatform("test1");
   WsfPlatform receivePlat = WsfSimulation.FindPlatform("test2");
   sendPlat.Comm("comm1").SendMessage(testMessage, receivePlat.Comm("comm1"));
end_execute

execute at_time 4.9 s absolute
   if(testPass)
   {
      writeln("-PASS-");
   }
   else
   {
      writeln("-FAIL-");
   }
end_execute

end_time 5 s

#event_output
#   enable all
#   file output.evt
#end_event_output



