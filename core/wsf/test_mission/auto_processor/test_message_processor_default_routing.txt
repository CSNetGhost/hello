# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

script_variables
 bool aMessageRcvd = false;
end_script_variables

comm RADIO WSF_COMM_TRANSCEIVER
end_comm

sensor 787_RADAR_SENSOR WSF_GEOMETRIC_SENSOR
   frame_time 1 sec
   reports_location
end_sensor

# -------------------------------------------------------------------------------------------------

platform 737 WSF_PLATFORM
   side green
   position 40n 90w altitude 30000 ft
end_platform

# -------------------------------------------------------------------------------------------------

platform 787 WSF_PLATFORM
   side blue
   position 39.1n 90w

   add processor msg_proc WSF_MESSAGE_PROCESSOR
	  default_routing
	     internal_link script_proc
	  end_default_routing
   end_processor

   add processor script_proc WSF_SCRIPT_PROCESSOR
      on_message
         default
            script
               extern bool aMessageRcvd;
               aMessageRcvd = true;
               writeln("TIME: ", TIME_NOW);
               writeln("script_proc received message.");
            end_script
      end_on_message
   end_processor

   add comm radio-1 RADIO
      on
      internal_link msg_proc
   end_comm

   add sensor airplane_scanner 787_RADAR_SENSOR
      on
	  update_interval 1 second
      internal_link msg_proc
   end_sensor
end_platform

execute at_time 5 seconds absolute
   extern bool aMessageRcvd;
   if (aMessageRcvd)
   {
      writeln("-PASS-");
   }
   else
   {
      writeln("TIME: ", TIME_NOW);
      writeln("-FAIL-");
   }
end_execute

end_time 60 sec
