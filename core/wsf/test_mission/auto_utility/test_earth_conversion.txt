# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# Test utility methods on Earth script class to convert between ECI and WCS coordinate systems

include ../auto_script/test_functions.txt
include ../auto_script/test_pass.txt

script_variables
   PassTestCount = 20;
end_script_variables

end_time 10 min

platform test WSF_PLATFORM
   position 12n 60w
   altitude 275 km
   add mover WSF_AIR_MOVER
      route
         turn_to_heading 30 deg
         speed 7740 m/s
      end_route
   end_mover
   
   execute at_interval_of 1 min
      Vec3 eci = LocationECI();
      Vec3 wcs = LocationWCS();
      Calendar now = WsfDateTime.CurrentTime();
   
      Vec3 convECI = Earth.ConvertWCS_ToECI(now, wcs);
      Vec3 convWCS = Earth.ConvertECI_ToWCS(now, eci);
   
      double eciDiffMag = Vec3.Subtract(eci, convECI).MagnitudeSquared();
      double wcsDiffMag = Vec3.Subtract(wcs, convWCS).MagnitudeSquared();
   
      if (AssertWithinToleranceMessage(0.0, eciDiffMag, 1.0e-4, write_str("TIME_NOW = ", TIME_NOW, " ECI diff not within tolerance")))
      {
         pass();
      }
   
      if (AssertWithinToleranceMessage(0.0, wcsDiffMag, 1.0e-4, write_str("TIME_NOW = ", TIME_NOW, " WCS diff not within tolerance")))
      {
         pass();
      }
   end_execute
end_platform
